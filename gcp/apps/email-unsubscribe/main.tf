# email-unsubscribe Cloud Run service
#
# App-specific configuration. Cloud Run and database infrastructure
# is generated by Terramate from gcp/apps/terramate.tm.hcl.

locals {
  app_name              = "email-unsubscribe"
  supabase_project_name = "mklv"

  gcp_secrets = provider::sops::file("${local.repo_root}/secrets/gcp.sops.json").data

  # Extra env vars for Cloud Run (secret_id references)
  extra_env_secrets = {
    ENCRYPTION_KEY       = module.oauth_secrets.secrets["email-unsubscribe-encryption-key"].secret_id
    GOOGLE_CLIENT_ID     = module.oauth_secrets.secrets["email-unsubscribe-oauth-client-id"].secret_id
    GOOGLE_CLIENT_SECRET = module.oauth_secrets.secrets["email-unsubscribe-oauth-client-secret"].secret_id
    GOOGLE_REDIRECT_URI  = module.oauth_secrets.secrets["email-unsubscribe-oauth-redirect-uri"].secret_id
  }
}

# OAuth Setup Instructions
# OAuth consent screen and credentials cannot be fully automated via Terraform.
# Manual steps required:
# 1. Configure OAuth consent screen:
#    https://console.cloud.google.com/apis/credentials/consent?project=mklv-infrastructure
#    - User Type: External
#    - App name: Email Unsubscribe
#    - Scopes: gmail.readonly, gmail.modify, gmail.labels
#    - Test users: Add your email
# 2. Create OAuth credentials:
#    https://console.cloud.google.com/apis/credentials?project=mklv-infrastructure
#    - Create Credentials â†’ OAuth client ID
#    - Application type: Web application
#    - Redirect URI: http://localhost:8000/oauth/callback
# 3. Store client ID and secret in secrets/gcp.sops.json (DONE)

# Enable Gmail API for OAuth email access
resource "google_project_service" "gmail" {
  disable_on_destroy = false
  project            = local.project_id
  service            = "gmail.googleapis.com"
}

# -----------------------------------------------------------------------------
# OAuth Secrets
# -----------------------------------------------------------------------------

module "oauth_secrets" {
  source = "${local.modules_dir}/gcp/secret-manager-secret"

  project_id = local.project_id
  secrets = {
    "email-unsubscribe-encryption-key"      = local.gcp_secrets.EMAIL_UNSUBSCRIBE_ENCRYPTION_KEY
    "email-unsubscribe-oauth-client-id"     = local.gcp_secrets.EMAIL_UNSUBSCRIBE_OAUTH_CLIENT_ID
    "email-unsubscribe-oauth-client-secret" = local.gcp_secrets.EMAIL_UNSUBSCRIBE_OAUTH_CLIENT_SECRET
    "email-unsubscribe-oauth-redirect-uri"  = local.gcp_secrets.EMAIL_UNSUBSCRIBE_OAUTH_REDIRECT_URI
  }
}

# Grant Cloud Run access to OAuth secrets
resource "google_secret_manager_secret_iam_member" "oauth_secrets" {
  for_each = module.oauth_secrets.secrets

  member    = "serviceAccount:${google_service_account.cloud_run.email}"
  project   = local.project_id
  role      = "roles/secretmanager.secretAccessor"
  secret_id = each.value.id
}
